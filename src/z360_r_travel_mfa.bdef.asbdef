managed;
strict ( 2 );
with draft;

define behavior for z360_r_travel_mfa alias Travel
implementation in class zbp_360_r_travel_mfa unique
persistent table z360_travel_mfa
draft table z360_travel_mfd
lock master
total etag LastChangedAt
authorization master ( instance, global )
etag master LocalLastChangedAt
{
  create ( precheck );
  update ( precheck );
  delete;
  association _Booking { create ( features : instance, authorization : update ); with draft; }


  field ( numbering : managed, readonly ) TravelUuid;
  field ( readonly ) TravelID,
  OverallStatus,
  TotalPrice,
  LastChangedAt,
  LocalCreatedAt,
  LocalCreatedBy,
  LocalLastChangedAt,
  LocalLastChangedBy;
  field ( mandatory ) CustomerID, CurrencyCode, AgencyID, BeginDate, EndDate;
  field ( features : instance ) BookingFee;

  determination setTravelNumber on save { create; }
  determination setStatusToOpen on modify { create; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  action ( features : instance, authorization : update ) acceptTravel result [1] $self;
  action ( features : instance, authorization : update ) rejectTravel result [1] $self;
  action ( features : instance, authorization : update ) deductDiscount
    parameter Z360_AE_DISCOUNT_MFA result [1] $self;
  internal action reCalcTotalPrice;

  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateBookingFee on save { create; field BookingFee; }

  determine action validateCustomerID { validation validateCustomer; }
  determine action validateAgencyID { validation validateAgency; }
  determine action validateDateRange { validation validateDates; }

  side effects
  {
    field BookingFee affects field TotalPrice;
    determine action validateCustomerID executed on field CustomerID affects messages;
    determine action validateAgencyID executed on field AgencyID affects messages;
    determine action validateDateRange executed on field BeginDate, field EndDate affects messages;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume with additional implementation;

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateAgency;
    validation validateDates;
    validation validateBookingFee;

    validation Booking~validateCustomer;
    validation Booking~validateConnection;
    validation Booking~validateCurrency;
    validation Booking~validateFlightPrice;
    validation Booking~validateStatus;

    validation Bksuppl~validateSupplement;
    validation Bksuppl~validateCurrency;
    validation Bksuppl~validatePrice;

  }

  mapping for z360_travel_a
    {
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

define behavior for z360_r_booking_mfa alias booking
implementation in class zbp_360_r_booking_mfa unique
persistent table z360_booking_mfa
draft table z360_booking_mfd
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  association _Bksuppl { create ( authorization : update ); with draft; }
  association _Travel { with draft; }

  field ( numbering : managed, readonly ) BookingUuid ;
  field ( readonly ) TravelUUID, BookingID, BookingDate, LocalLastChangedAt;
  field ( mandatory ) CustomerID, AirlineID, ConnectionID, FlightDate, FlightPrice, CurrencyCode;

  determination setBookingNumber on save { create; }
  determination setBookingDate on save { create; }
  determination calculateTotalPrice on save { create; field FlightPrice, CurrencyCode; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateConnection on save { create; field AirlineID, ConnectionID, FlightDate; }
  validation validateCurrency on save { create; field CurrencyCode; }
  validation validateFlightPrice on save { create; field FlightPrice; }
  validation validateStatus on save { create; field BookingStatus; }

  determine action validateFlightCosts { validation validateFlightPrice; }

  side effects
  {
    field FlightPrice affects field _Travel.TotalPrice;
    determine action validateFlightCosts executed on field CustomerID affects messages;
  }

  mapping for z360_booking_a
    {
      BookingUUID        = booking_uuid;
      TravelUUID         = parent_uuid;
      BookingID          = booking_id;
      BookingDate        = booking_date;
      CustomerID         = customer_id;
      AirlineID          = carrier_id;
      ConnectionID       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for z360_r_bksuppl_mfa alias Bksuppl
implementation in class zbp_360_r_bksuppl_mfa unique
persistent table z360_bksuppl_mfa
draft table z360_bksuppl_mfd
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;


  association _Travel { with draft; }
  association _Booking { with draft; }

  field ( numbering : managed, readonly ) BooksupplUuid;
  field ( readonly ) TraverlUuid, BookingUUID, BookingSupplementID, LocalLastChangedAt;
  field ( mandatory ) SupplementID, Price, CurrencyCode;

  determination setBookingSupplNumber on save { create; }
  determination calculateTotalPrice on save { create; field Price, CurrencyCode; }

  validation validateSupplement on save { create; field SupplementID; }
  validation validateCurrency on save { create; field CurrencyCode; }
  validation validatePrice on save { create; field Price; }

  determine action validateSupplementPrice { validation validatePrice; }

  side effects
  {
    field Price affects field _Travel.TotalPrice;
    determine action validateSupplementPrice executed on field Price affects messages;
  }

  mapping for z360_bksuppl_mfa
    {
      BookSupplUUID       = booksuppl_uuid;
      TraverlUuid          = root_uuid;
      BookingUUID         = parent_uuid;
      BookingSupplementID = booking_supplement_id;
      SupplementID        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
    }

}